plugins {
    id 'java'
    id 'org.springframework.boot' version '2.5.12'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id "io.freefair.lombok" version "6.5.0.2"
    id "checkstyle"
    id "com.github.spotbugs" version "4.8.0"
    id "org.sonarqube" version "4.4.1.3373"
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'checkstyle'
apply plugin: "com.github.spotbugs"

group = 'uk.nhs.digital.nhsconnect'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.retry:spring-retry:1.3.1'
    implementation 'ca.uhn.hapi.fhir:hapi-fhir-base:5.4.0'
    implementation 'ca.uhn.hapi.fhir:hapi-fhir-structures-r4:5.4.0'
    implementation 'ca.uhn.hapi.fhir:hapi-fhir-validation-resources-r4:5.4.0'
    implementation 'com.github.spullara.mustache.java:compiler:0.9.10'
    implementation 'org.apache.httpcomponents:httpclient:4.5.13'
    implementation 'org.apache.commons:commons-text:1.9'
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'org.springframework.boot:spring-boot-starter-webflux:2.7.1'

    implementation 'ch.qos.logback:logback-classic:1.2.6'
    implementation 'ch.qos.logback.contrib:logback-json-classic:0.1.5'
    implementation 'ch.qos.logback.contrib:logback-jackson:0.1.5'
    implementation 'com.vdurmont:emoji-java:5.1.1'

    testImplementation 'org.projectlombok:lombok'
    testImplementation "org.mockito:mockito-core:4.0.0"
    testImplementation "org.assertj:assertj-core:3.21.0"
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation "io.rest-assured:rest-assured:4.4.0"
    testImplementation "io.rest-assured:json-path:4.4.0"
    testImplementation "io.rest-assured:xml-path:4.4.0"
    testImplementation "com.github.tomakehurst:wiremock-jre8-standalone:2.31.0"
}

test {
    useJUnitPlatform()
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }

        resources {
            srcDir file('src/integration-test/resources')
        }
    }
}

configurations {
    integrationTestCompileOnly.extendsFrom testCompileOnly
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
    integrationTestAnnotationProcessor.extendsFrom testAnnotationProcessor
}

task integrationTest(type: Test) {
    useJUnitPlatform() {
        description = 'Runs integration tests.'
        group = 'verification'

        testClassesDirs = sourceSets.integrationTest.output.classesDirs
        classpath = sourceSets.integrationTest.runtimeClasspath
        shouldRunAfter test
    }
}

task staticCodeAnalysis(type: GradleBuild) {
    tasks = [
            'clean',
            'checkstyleMain',
            'checkstyleTest',
            'checkstyleIntegrationTest',
            'spotbugsMain',
            'spotbugsTest',
            'spotbugsIntegrationTest',
    ]
}

check.dependsOn integrationTest

tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
    excludeFilter = rootProject.file('config/spotbugs/exclude.xml')
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

sonar {
    properties {
        property "sonar.projectKey", "NHSDigital_summary-care-record-api"
        property "sonar.organization", "nhsdigital"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.gradle.skipCompile", true
        property "sonar.login", "$System.env.SONAR_TOKEN"
    }
}
